#!/bin/zsh

# See ~/.shared_env.d/ssh

_pre_ssh () {
    dsa
    # Pick out user@host word from argv; if it's not found, default to $1.
    # Finally, strip off .* domain component, if any.
    cx "${${${(M@)argv:#*@*}:-$1}%%.[a-z]*}" 

    [[ -z "$sshargs" ]] && sshargs=()
}

ssh () {
    setopt local_traps
    trap 'cxx' INT EXIT QUIT KILL
    _pre_ssh "$@"
    command ssh "$sshargs[@]" "$@"
}

mosh () {
    setopt local_traps
    trap 'cxx' INT EXIT QUIT KILL
    _pre_ssh "$@"
    command mosh "$sshargs[@]" "$@"
}

sshx () {
    oldterm="$TERM"
    TERM=xterm ssh "$@"
    TERM="$oldterm"
}

ssh2 () {
    if [ -n "$SSH_ORIG_CLIENT" ]; then
        # see ~/.shared_rc.d/ssh
        export LC_TELEPHONE_ORIG="$LC_TELEPHONE"
        export LC_TELEPHONE="${LC_TELEPHONE}^^export SSH_ORIG_CLIENT=${SSH_ORIG_CLIENT}"
        sshargs=( -o SendEnv=LC_TELEPHONE )
        #sshargs=( -o SendEnv=SSH_ORIG_CLIENT )
    fi

    ssh "$@"

    if [ "$+LC_TELEPHONE_ORIG" = 1 ]; then
        LC_TELEPHONE="$LC_TELEPHONE_ORIG"
    fi
}
