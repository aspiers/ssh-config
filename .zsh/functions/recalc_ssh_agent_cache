#autoload

export SSH_AGENT_PID=
export SSH_AUTH_SOCK=

echo "Recalculating ssh-agent info ..."

if ! which rj ps >&/dev/null; then
  echo 'rj and ps must both be present on $PATH; aborting.'
  return 1
fi

local pids
pids=(
  $( rj | awk "/^ *($USER|$UID) "'.*ssh-agent/ && ! /awk/ { print $2 }' )
)

if (( $#pids > 1 )); then
  echo "$#pids pids found ($pids); aborting."
  echo "Please ensure only one agent is running."
  return 1
fi

SSH_AGENT_PID=$pids[0]

if [[ "$SSH_AGENT_PID" != *[0-9]* ]]; then
  # If interactive, prompt whether to start a new one.
  # UPDATE: don't prompt; I always answer yes.
  if false && [[ -t 1 ]]; then
    echo "ssh-agent process not found; start a new one [y/N] ?"
    if read -q; then
      eval `ssh-agent`
      write_ssh_agent_cache
      return 0
    else
      echo "Unable to determine ssh-agent info; aborting."
      return 1
    fi
  else
    eval `ssh-agent`
    write_ssh_agent_cache
    return 0
  fi
fi

# : ${SSH_AUTH_SOCK:=`lsof -p $SSH_AGENT_PID |
#      awk '/agent-socket|^ssh-agent.*unix/ {print $NF; exit}'`}

# if [ -z "$SSH_AUTH_SOCK" ]; then
#   echo "Huh? lsof didn't do the biz; aborting ..."
#   return 1
# fi

_sockets=( /tmp/ssh-*/*(=U) )

if (( $#_sockets == 0 )); then
  echo "No sockets found; aborting."
  return 1
fi

local -a _dead_sockets _live_sockets
echo "$#_sockets _sockets found:"
for s in "${_sockets[@]}"; do
  _lsof="`lsof $s`"
  if [[ -z "$_lsof" ]]; then
    echo "  $s appears dead."
    _dead_sockets=( "${_dead_sockets[@]}" "$s" )
  elif [[ "$_lsof" == *sshd* ]]; then
    echo "  $s used for agent forwarding:"
    echo "${(F@)${(f)_lsof}//(#s)/    }" # see below
  else
    echo "  $s in use:"
    echo "${(F@)${(f)_lsof}//(#s)/    }"
    # does the same as:
    #   echo "$lsof" | sed -e 's/^/    /'
    # by splitting at \n into an array (f), substituting start
    # of each element (#s) for '    ' and rejoining with \n (F).
    _live_sockets=( "${_live_sockets[@]}" "$s" )
  fi
done

if (( $#_dead_sockets > 0 )); then
  echo "Removing dead sockets:\n  rm $_dead_sockets"
  rm "$_dead_sockets[@]"
fi

if (( $#_live_sockets != 1 )); then
  echo "BUG?  Didn't find a unique live socket" >&2
  return 1
fi

: ${SSH_AUTH_SOCK:=$_live_sockets[0]}
write_ssh_agent_cache
return 0
