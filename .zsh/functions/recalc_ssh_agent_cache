#autoload

export SSH_AGENT_PID=
export SSH_AUTH_SOCK=

echo "Recalculating ssh-agent info ..."

if ! which rj ps >&/dev/null; then
  echo 'rj and ps must both be present on $PATH; aborting.'
  return 1
fi

local pids
pids=(
  $( rj | awk "/^ *($USER|$UID) "'.*ssh-agent/ && ! /awk/ { print $2 }' ) 
)

if (( $#pids > 1 )); then
  echo "$#pids pids found ($pids); aborting."
  echo "Please ensure only one agent is running."
  return 1
fi

SSH_AGENT_PID=$pids[0]

if [[ "$SSH_AGENT_PID" != *[0-9]* ]]; then
  # If interactive, prompt whether to start a new one.
  # UPDATE: don't prompt; I always answer yes.
  if false && [[ -t 1 ]]; then
    echo "ssh-agent process not found; start a new one [y/N] ?"
    if read -q; then
      eval `ssh-agent`
      write_ssh_agent_cache
      return 0
    else
      echo "Unable to determine ssh-agent info; aborting."
      return 1
    fi
  else
    eval `ssh-agent`
    write_ssh_agent_cache
    return 0
  fi
fi

# : ${SSH_AUTH_SOCK:=`lsof -p $SSH_AGENT_PID |
#      awk '/agent-socket|^ssh-agent.*unix/ {print $NF; exit}'`}

# if [ -z "$SSH_AUTH_SOCK" ]; then
#   echo "Huh? lsof didn't do the biz; aborting ..."
#   return 1
# fi

sockets=( /tmp/ssh-*/*(=U) )
if (( $#sockets == 0 )); then
  echo "No sockets found; aborting."
  return 1
elif (( $#sockets > 1 )); then
  local -a _dead_sockets
  echo "$#sockets sockets found:"
  for s in "${sockets[@]}"; do
    _lsof="`lsof $s`"
    if [[ -z "$_lsof" ]]; then
      echo "  $s appears dead."
      _dead_sockets=( "${_dead_sockets[@]}" "$s" )
    else
      echo "  $s in use:"
      echo "${(F@)${(f)_lsof}//(#s)/    }"
      # does the same as:
      #   echo "$lsof" | sed -e 's/^/    /'
      # by splitting at \n into an array, substituting start
      # of each element for '    ' and rejoining with \n.
    fi
  done
  if (( $#_dead_sockets > 0 )); then
    echo "Action:\n  rm $_dead_sockets"
    rm "$_dead_sockets[@]"
  fi
  return 1
fi
: ${SSH_AUTH_SOCK:=$sockets[0]}

write_ssh_agent_cache
return 0
