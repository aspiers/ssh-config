#!/usr/bin/perl

use strict;
use warnings;

use Net::Domain qw(hostfqdn);
use Sh qw(cat);
use Socket;

sub usage {
  warn @_, "\n" if @_;

  (my $ME = $0) =~ s,.*/,,;

  die <<EOUSAGE;
Usage: $ME [SRCHOST]
EOUSAGE
}

usage() if @ARGV > 1;

my ($src_IP, $src_port, $dst_IP, $dst_port);
if (@ARGV) {
  $src_IP = $ARGV[0];

  # inet_ntoa(scalar gethostbyname $fqdn) can return 127.0.0.2 :-(
  my $fqdn = hostfqdn();
  ($dst_IP) = `host $fqdn` =~ /has address (\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3})/;
  die "Couldn't determine dest IP via 'host $fqdn'" unless $dst_IP;
}
else {
  if (exists $ENV{SSH_CONNECTION}) {
    # Source is the server initiating the remote connection.
    # Destination is the server we are running this script on.
    ($src_IP, $src_port, $dst_IP, $dst_port)
      = split /\s+/, $ENV{SSH_CONNECTION};
  }
  else {
    print "local\n";
    exit 0;
  }
}

my $dir = "$ENV{HOME}/.connection-type/$src_IP/$dst_IP";
#print "$dir\n";

if (-e "$dir/type") {
  print cat("$dir/type");
  exit 0;
}
if (-e "$dir/guessed-type") {
  print cat("$dir/guessed-type");
  exit 0;
}

print "unknown\n";
exit 1;
